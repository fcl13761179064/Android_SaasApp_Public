apply plugin: 'com.android.application'
apply plugin: 'au.com.dius.pact'
apply plugin: 'android-aspectjx'

// Create a variable called keystorePropertiesFile, and initialize it to your
// keystore.properties file, in the rootProject folder.
def keystorePropertiesFile = rootProject.file("keystore.properties")

// Initialize a new Properties() object called keystoreProperties.
def keystoreProperties = new Properties()

// Load your keystore.properties file into the keystoreProperties object.
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "com.ayla.hotelsaas"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode Integer.parseInt(new Date().format("yyyyMMdd"))
        versionName "1.5.0"
        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        ndk {
            // 设置支持的SO库架构
            // abiFilters 'armeabi', 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
            abiFilters "arm64-v8a", "armeabi-v7a"  //阿里云的sdk api8以上就需要静止2个so文件
        }
        lintOptions {
            abortOnError false
        }
    }

    signingConfigs {
        config {
            v1SigningEnabled true
            v2SigningEnabled true
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    flavorDimensions "server_domain"

    productFlavors {
        dev {//开发服务地址
            buildConfigField("String", "server_domain", "\"dev\"")
            dimension "server_domain"
        }
        qa {//测试服务地址
            buildConfigField("String", "server_domain", "\"qa\"")
            dimension "server_domain"
        }
        prod {//量产服务地址
            buildConfigField("String", "server_domain", "\"prod\"")
            dimension "server_domain"
        }
    }

    buildTypes {
        release {
            zipAlignEnabled true
            shrinkResources true  // 移除无用的resource文件
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
        debug {
            signingConfig signingConfigs.config
        }
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
}


task replaceAliPicture() {
    String taskName = gradle.startParameter.taskNames[0]
    if (taskName != null) {
        if (taskName.contains("assembleProd")) {//使用正式图片
            println("replaceAliPicture:当前替换阿里安全图片为pro")
            copy {
                from("${buildFile.parent}/ali_pic/pro/yw_1222_china_production.jpg")
                into("${buildFile.parent}/src/main/res/drawable")
            }
        } else {//使用开发图片
            println("replaceAliPicture:当前替换阿里安全图片为dev")
            copy {
                from("${buildFile.parent}/ali_pic/dev/yw_1222_china_production.jpg")
                into("${buildFile.parent}/src/main/res/drawable")
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar', '*.aar'])
    implementation 'com.github.bumptech.glide:glide:4.9.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.9.0'
    implementation 'cn.bingoogolapple:bga-qrcode-zbar:1.3.7'
    implementation 'com.github.tbruyelle:rxpermissions:0.10.2'
    testImplementation 'junit:junit:4.12'
    testImplementation("au.com.dius:pact-jvm-consumer-junit:4.0.3")
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.1'
    implementation rootProject.libraries.butterknife
    annotationProcessor rootProject.libraries.butterknife_compiler
    implementation rootProject.libraries.rxjava
    implementation rootProject.libraries.rxandroid
    implementation rootProject.libraries.retrofit
    implementation rootProject.libraries.converter_gson
    implementation rootProject.libraries.logging_interceptor
    implementation rootProject.libraries.adapter_rxjava
    implementation rootProject.libraries.autosize
    implementation rootProject.libraries.SmartRefreshLayout
    implementation rootProject.libraries.SmartRefreshHeader
    implementation 'com.scwang.smart:refresh-layout-kernel:2.0.1'
    implementation  'com.scwang.smart:refresh-header-classics:2.0.1'    //经典刷新头
    implementation  'com.scwang.smart:refresh-footer-classics:2.0.1'    //经典加载
    implementation rootProject.libraries.BaseRyViewAdapHelper
    implementation rootProject.libraries.utilcode
    implementation rootProject.libraries.eventbus
    implementation 'com.xw.repo:bubbleseekbar:3.20'
    implementation 'com.tencent.bugly:crashreport:3.1.9'
    implementation 'com.github.wendux:DSBridge-Android:x5-3.0-SNAPSHOT'
    implementation 'com.github.AylaAsia:HongYanConfigLib:0.0.8'
}

pact {
    publish {
        // 契约地址
        pactDirectory = "app/target/pacts"
        //broker url
        pactBrokerUrl = "https://abp-test.ayla.com.cn"
        pactBrokerUsername = 'admin'
        pactBrokerPassword = 'adminPassword'

        version = '1.0.0'
    }
}